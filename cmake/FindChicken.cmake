# - Find Chicken module

include(FeatureSummary)
include(FindPackageHandleStandardArgs)
include(FindPackageMessage)

set_package_properties(Chicken PROPERTIES
    DESCRIPTION "A practical and portable Scheme system"
    URL "http://call-cc.org")

# Do not show error messages if the package was not found: there might be no
# CMake aware Chicken installed or we are cross-compiling, try to quetly guess
# the paths instead, this can also pull config file from install prefix, but it
# should not cause any harm.
find_package(Chicken QUIET CONFIG)

find_program(CHICKEN_COMPILER chicken
    CMAKE_FIND_ROOT_PATH_BOTH)

find_program(CHICKEN_INTERPRETER csi
    CMAKE_FIND_ROOT_PATH_BOTH)

find_path(CHICKEN_INCLUDE_DIR chicken.h
    HINTS $ENV{CHICKEN_PREFIX}/include
    PATH_SUFFIXES chicken
    CMAKE_FIND_ROOT_PATH_BOTH)

find_library(CHICKEN_LIBRARY chicken
    HINTS $ENV{CHICKEN_PREFIX}/lib
    CMAKE_FIND_ROOT_PATH_BOTH)

get_filename_component(_chicken_prefix ${CHICKEN_INCLUDE_DIR} PATH)
get_filename_component(_chicken_lib_dir ${CHICKEN_LIBRARY} PATH)
if(${_chicken_prefix})
    string(REGEX REPLACE "^(.*)/include$" "\\1" _chicken_prefix ${_chicken_prefix})
endif()

find_library(CHICKEN_STATIC_LIBRARY libchicken.a
    HINTS ${_chicken_lib_dir}
    NO_SYSTEM_ENVIRONMENT_PATH
    NO_CMAKE_SYSTEM_PATH)

if(WIN32)
    # TODO: add check for x64
    set(CHICKEN_EXTRA_LIBRARIES ws2_32)
elseif(ANDROID)
    set(CHICKEN_EXTRA_LIBRARIES m;log)
else()
    set(CHICKEN_EXTRA_LIBRARIES m)
endif()
list(APPEND CHICKEN_EXTRA_LIBRARIES ${CMAKE_DL_LIBS})

# These used to be options, but caused various problems and are hardcoded for
# now.
set(CHICKEN_DEFINITIONS HAVE_CHICKEN_CONFIG_H C_ENABLE_PTABLES)
if(MSVC)
    # Chicken causes a lot of these
    list(APPEND CHICKEN_DEFINITIONS _CRT_SECURE_NO_WARNINGS)
elseif(UNIX AND NOT APPLE)
    # Added in glibc 2.19, since 2.20 issues a warning if this is not defined.
    list(APPEND CHICKEN_DEFINITIONS _DEFAULT_SOURCE)
endif()

if(MSVC)
    # C4101 - unreferenced local variable
    # C4996 - The POSIX name for this item is deprecated
    set(_chicken_c_flags /wd4101 /wd4996)
else()
    # Felix wrote, that these are required. Strict aliasing caused some real
    # problems and wrapv disables some questionable aggressive compiler
    # optimizations with regard to signed integer overflow.
    set(_chicken_c_flags -fno-strict-aliasing -fwrapv)
endif()

if(APPLE)
    list(APPEND _chicken_c_flags -fno-common)
endif()

set(CHICKEN_C_FLAGS ${_chicken_c_flags} CACHE STRING
    "C compiler flags which are added to every file generated by Chicken")
mark_as_advanced(CHICKEN_C_FLAGS)

# CMake module guide recommends to use plural variable names for consistency.
set(CHICKEN_INCLUDE_DIRS ${CHICKEN_INCLUDE_DIR})
set(CHICKEN_LIBRARIES ${CHICKEN_LIBRARY} ${CHICKEN_EXTRA_LIBRARIES})
set(CHICKEN_STATIC_LIBRARIES ${CHICKEN_STATIC_LIBRARY} ${CHICKEN_EXTRA_LIBRARIES})

# Consider Chicken found if we determined that at least the executable is
# available. Probably more comprehensive logic is needed to warn a user that
# configuration is incomplete but it causes problems when bootstrapping.
find_package_handle_standard_args(Chicken DEFAULT_MSG CHICKEN_COMPILER)

# Various macros for user CMakeLists.
include(${CMAKE_CURRENT_LIST_DIR}/ChickenUse.cmake)
